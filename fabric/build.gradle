plugins {
    id 'com.gradleup.shadow'
    id "me.modmuss50.mod-publish-plugin"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'Ladysnake Mods'
        url = 'https://maven.ladysnake.org/releases'
    }
    maven {
        name = 'GeckoLib'
        url = 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
        content {
            includeGroupByRegex("software\\.bernie.*")
            includeGroup("com.eliotlash.mclib")
        }
    }
    maven {
        name = "Fuzs Mod Resources"
        url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
    }
    maven { url = "https://maven.terraformersmc.com/releases" }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:$fabric_api_version"

    // Architectury API. This is optional, and you can comment it out if you don't need it.
    modImplementation "dev.architectury:architectury-fabric:$architectury_api_version"

    include(annotationProcessor(implementation("io.github.llamalad7:mixinextras-fabric:0.4.1")))

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')

    modImplementation("org.valkyrienskies:valkyrienskies-120-fabric:$vs2_version") {
        exclude group: "com.simibubi.create"
        exclude group: "net.createmod.ponder"
        // Excluding sodium since it crashes with vs2 for some reason
        exclude group: 'maven.modrinth', module: 'sodium'
        exclude group: "io.github.fabricators_of_create.PortingLib"
        exclude group: "com.tterrag.registrate_fabric"
        exclude group: "dev.engine-room.flywheel"
    }
    implementation("org.valkyrienskies.core:api:$vs_core_version") {
        exclude group: 'org.joml', module: ''
    }
    implementation("org.valkyrienskies.core:util:$vs_core_version") {
        exclude group: 'org.joml', module: ''
    }
    implementation("org.valkyrienskies.core:internal:$vs_core_version") {
        exclude group: 'org.joml', module: ''
    }

    // Kotlin
    modImplementation("net.fabricmc:fabric-language-kotlin:$kotlin_version")

    api "org.jetbrains.kotlin:kotlin-stdlib:2.0.0"
    api "org.jetbrains.kotlin:kotlin-reflect:2.0.0"


    implementation("org.joml:joml-primitives:1.10.0")
    implementation("org.joml:joml:1.10.4")

    // TODO: fabric runtime dep
}

processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand version: inputs.properties.version, architectury_api_version: architectury_api_version,
                vs2_version: (vs2_version.contains("+") ? vs2_version.substring(0, vs2_version.indexOf("+")) : vs2_version)
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}